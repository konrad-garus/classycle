// Based on https://github.com/gradle/gradle/blob/master/gradle/classycle.gradle
allprojects {
    ext.useClassycle = { params = [:] ->
        def excludePatterns = params.exclude ?: []

        configurations {
            classycle
        }
        
        // The only one in Maven Central is in org.specs2 and it does not seem to include Ant.
        repositories {
            maven { url 'https://raw.githubusercontent.com/konrad-garus/classycle/master' }
        }

        dependencies {
            classycle ('com.oasisdigital:classycle:1.4.2')
        }

        task classycle

        sourceSets.all { sourceSet ->
            def taskName = sourceSet.getTaskName('classycle', null)
            task(taskName) {
                def reportFile = reporting.file("classcycle/${sourceSet.name}.txt")
                def definitionFile = file("src/test/resources/classycle-${sourceSet.name}.txt")
                inputs.files sourceSet.output, definitionFile
                outputs.file reportFile
                doLast {
                    if (!sourceSet.output.classesDir.directory) {
                        return;
                    }
                    ant.taskdef(name: "classycleDependencyCheck", classname: "classycle.ant.DependencyCheckingTask", classpath: configurations.classycle.asPath)
                    reportFile.parentFile.mkdirs()
                    try {
                        ant.classycleDependencyCheck(
                            reportFile: reportFile,
                            failOnUnwantedDependencies: true,
                            mergeInnerClasses: true,
                            definitionFile: definitionFile) {
                            fileset(dir: sourceSet.output.classesDir) {
                                excludePatterns.each { excludePattern ->
                                    exclude(name: excludePattern)
                                }
                            }
                        }
                    } catch(Exception e) {
                        throw new RuntimeException("Classycle check failed: $e.message. See report at ${new org.gradle.logging.ConsoleRenderer().asClickableFileUrl(reportFile)}", e)
                    }
                }
            }
            classycle.dependsOn taskName
            check.dependsOn classycle
        }
    }
}

